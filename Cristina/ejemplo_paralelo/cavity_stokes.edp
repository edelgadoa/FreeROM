// FreeFem++ cavity_stokes.edp


macro dimension() 2 // EOM
include "macro_ddm.idp";

// Parameter definition
real epspen=1.e-8;
real Re=1500.;

// Macro definition
macro defu(u) [u,u#Y,u#P] // EOM
macro gradgrad(u,v) (dx(u)*dx(v)+dy(u)*dy(v)+dx(u#Y)*dx(v#Y)+dy(u#Y)*dy(v#Y))// EOM
macro div(u)(dx(u) + dy(u#Y))// EOM

// Mesh creation
meshN Th;
fespace Vh(Th,[P2,P2,P1]);
int walls = 1,top = 2;
int NN = 50;
{
	real x01 = 0., x11 = 1., y01 = 0., y11 = 1.;
	int [int] icase = [walls,walls,top,walls];
	Th = square(NN,NN,[x01+(x11-x01)*x,y01+(y11-y01)*y],label = icase, flags=3);
}
plot(Th);

// Problem definition
Vh defu(u);
matrix A;
real [int] b(Vh.ndof);
{
	varf a(defu(u),defu(v))=intN(Th)(
							1./Re*gradgrad(u,v)
							-uP*div(v)
							+vP*div(u)
							+uP*vP*epspen
							)
							+on(top,u=1,uY=0)
							+on(walls,u=0,uY=0)
							;
	A=a(Vh,Vh);
	b=a(0,Vh);
}

// Resolution
set(A,solver=sparsesolver);

u[]=A^-1*b;
plot([u,uY]);
plot(uP,fill=1);

///////////////////////////////////////////////////////////////////////////////////