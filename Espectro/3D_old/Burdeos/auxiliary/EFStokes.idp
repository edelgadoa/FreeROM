
NewMacro EFSta(uEF,Re)
{
	real Reynold=Re[0];
	include "../auxiliary/search.idp";
	if(search(ReSta,Re,1.e-9)==-1)
	{
		matrix ASta;real[int] bSta(VhLVP.ndof);
		{
			varf a(defVP(u),defVP(v))=
				intN(ThL)(
					1./Reynold*gradgrad(u,v)
					-uP*div(v)
					+vP*div(u)
		   			+uP*vP*epspen
		   			 )
		   		-intN(ThL)(
		   			 1./Reynold*gradgrad(G,v)
		   			 +vP*div(G)
		   			 )
				 +BC(u,cero);
			ASta=a(VhLVP,VhLVP);bSta=a(0,VhLVP);
		}
		solvesystem(ASta,bSta,uEF,VP);
		write("../solutions/txt/SolEF"+prob+"NN"+NN+"Reynold"+Reynold+".txt",uEF[],0,0,mpirank,20);
		updateparameters(ReSta,Re);
	}
	else
	{
		write("Output.txt","   Reading the solution...",1,1);
		ifstream fileRe("../solutions/txt/SolEF"+prob+"NN"+NN+"Reynold"+Reynold+".txt");
		fileRe >> uEF[];
	}
	// plot(uEFP,value=1,fill=1);
}
EndMacro

NewMacro residualb(sol,Reynold,residual)
{
	VhGVP defVP(u);
	u[]=sol[];
	u[]+=G[];
	// plot(defVel(u),value=1,wait=0);
	matrix Ares;real[int] bres(VhLVP.ndof);
	{
		varf a(defVP(u0),defVP(v))=
			intN(ThL)(1./Refijo*gradgrad(u0,v)+u0P*vP)
			+intN(ThL)(
				1./Reynold*gradgrad(u,v)
				-u#P*div(v)
				+vP*div(u)
				+u#P*vP*epspen
	   			 )
			 +BC(u0,cero);
		Ares=a(VhLVP,VhLVP);bres=a(0,VhLVP);
	}
	solvesystem(Ares,bres,residual,VP);
}
EndMacro

NewMacro supremizer(uEF,Re,sol)
{
	matrix Asup;real[int] bsup(VhLVel.ndof);
	{
		varf a(defVel(u),defVel(v))=
			intN(ThL)(1./Refijo*gradgrad(u,v))
			+intN(ThL)(-uEF#P*div(v))
			+BC(u,cero);			
		Asup=a(VhLVel,VhLVel);bsup=a(0,VhLVel);
	}
	solvesystem(Asup,bsup,sol,Vel);
}
EndMacro

macro matrixprodV(prodV)
matrix prodV;
{
	varf a(defVel(u),defVel(v))=
		intN(Th)(1./Refijo*gradgrad(u,v));
	prodV=a(VhGVel,VhGVel);
}
// EOM
macro matrixprodP(prodP)
matrix prodP;
{
	varf a(u,v)=
		intN(Th)(u*v);
	prodP=a(VhGP1,VhGP1);
}
// EOM
macro matrixprodVP(prodVP)
matrix prodVP;
{
	varf a(defVP(u),defVP(v))=
		intN(Th)(1./Refijo*gradgrad(u,v)+uP*vP);
	prodVP=a(VhGVP,VhGVP);
}
// EOM
macro matrixprodL2(prodL2)
matrix prodL2;
{
	varf a(defVP(u),defVP(v))=
		intN(Th)(uporv(u,v));
	prodL2=a(VhGVP,VhGVP);
}
// EOM
macro matrixprodH1(prodH1)
matrix prodH1;
{
	varf a(defVP(u),defVP(v))=
		intN(Th)(gradgrad(u,v));
	prodH1=a(VhGVP,VhGVP);
}
// EOM
NewMacro matrixgeneration();
matrixprodV(prodV);matrixprodP(prodP);matrixprodVP(prodVP);
matrix matrixStoNnu;
{
	varf a(defVel(u),defVel(v))=intN(Th)(gradgrad(u,v));
	matrixStoNnu=a(VhGVel,VhGVel);
}
matrix matrixStoNdiv;
{
	varf a(defVel(u),[p])=intN(Th)(div(u)*p);
	matrixStoNdiv=a(VhGVel,VhGP1);
}
matrix matrixStoNpen;
{
	varf a(u,v)=intN(Th)(epspen*u*v);
	matrixStoNpen=a(VhGP1,VhGP1);
}
EndMacro