///////////////
// FreeROM 
/////////////// 

// Programa que calcula el residuo de la soluci√≥n de RBM
Mat MRes;
real[int] bRes(Vh4P2.ndof);

// Termino de Navier-Stokes
real[int] bNSRes(Vh4P2.ndof);


{
	varf NSRes([u,uY,uZ,uP],[v,vY,vZ,vP]) = int3d(Th)((1./Refijo + MOD(uFijo))*GradUGradV(u,v) + uP*vP)
											// Segundo miembro
										  + int3d(Th)(nuRB*GradUGradV(uRB,v) + Antisimetric(uRB,uRB,v) - Div(v)*uRBP + Div(uRB)*vP)
										  + on(1,2, u=0, uY=0, uZ=0);
										  
    matrix matrixRes = NSRes(Vh4P2, Vh4P2);
	
	macro def(i) [i, i#Y, i#Z, i#P] //EOM
	macro init(i) [i, i, i, i] //EOM
	MatCreate(Th, MRes, [P2,P2,P2,P2]);
	
	MRes = matrixRes;
	set(MRes, sparams = "-pc_type lu -ksp_type gmres -ksp_converged_reason"); // TODO: probar con otros parametros									
	bNSRes = NSRes(0, Vh4P2);
}
								
// Termino residuo VMS-Smagorinsky
real[int] bSmagoRes(Vh4P2.ndof); 

{
	real[int] bSx(Vh4P2.ndof), bSy(Vh4P2.ndof), bSz(Vh4P2.ndof), bSaux(Vh4P1dc.ndof);
	VhP1dc uFRB, uFRBY, uFRBZ;
	VhP1dc GrN = MOD(uRB);
	varf SmagoRes([u,uY,uZ,uP],[v,vY,vZ,vP]) = int3d(Th)((CS*hTriangle)^2*GrN*UporV(uFRB,v));
	
	// Componente X
	uFRB = uRBdX; uFRBY = uRBYdX; uFRBZ = uRBZdX;
	bSaux = SmagoRes(0, Vh4P1dc);
	bSx = DX4P2IPh'*bSaux;
	
	// Componente Y
	uFRB = uRBdY; uFRBY = uRBYdY; uFRBZ = uRBZdY;
	bSaux = SmagoRes(0, Vh4P1dc);
	bSy = DY4P2IPh'*bSaux;
	
	// Componente Z
	uFRB = uRBdZ; uFRBY = uRBYdZ; uFRBZ = uRBZdZ;
	bSaux = SmagoRes(0, Vh4P1dc);
	bSz = DZ4P2IPh'*bSaux;
	
	bSmagoRes = bSx + bSy;
	bSmagoRes += bSz;
	bSmagoRes = bSmagoRes.*cc; // bloqueo de las condiciones de contorno

}

//mpi(cout<<bSmagoRes<<endl;)
// Termino LPS
real[int] bStabPRes(Vh4P2.ndof);

{
	VhP0 tKres, tKTres, taures;
	varf tauKres(un,v) = int3d(Th)(UporV(uRB, uRB)*v);
	varf tauKTres(un,v) = int3d(Th)((MOD(uRB)^2)*v);
	real[int] tK = tauKres(0, VhP0);
	real[int] tKT = tauKTres(0, VhP0);
	tKres[] = sqrt(tK); tKTres[] = sqrt(tKT);
	
	taures = ((cc1*(nuRB+((CS*hTriangle)^2)*(tKTres/mk))/hTriangle^2) + (cc2*(tKres/mk)/hTriangle))^(-1.); 
	
	VhP1dc uFRBP;
	varf StabPRes(unP,vP) = int3d(Th)(taures*uFRBP*vP);
	
	real[int] bSPx(Vh4P2.ndof), bSPy(Vh4P2.ndof), bSPz(Vh4P2.ndof), bSPaux(VhP1dc.ndof);
	// Componente X
	uFRBP[] = IPhDXP2 * uRBP[];
	bSPaux = StabPRes(0, VhP1dc);
	bSPx = IPhD4X4P2'*bSPaux;  
	
	// Componente Y
	uFRBP[] = IPhDYP2 * uRBP[];
	bSPaux = StabPRes(0, VhP1dc);
	bSPy = IPhD4Y4P2'*bSPaux;  
	
	// Componente Z
	uFRBP[] = IPhDZP2 * uRBP[];
	bSPaux = StabPRes(0, VhP1dc);
	bSPz = IPhD4Z4P2'*bSPaux; 

	bStabPRes = bSPx + bSPy; 
	bStabPRes += bSPz;
}
//mpi(cout<<bStabPRes<<endl;)

bRes = bNSRes;
bRes += bSmagoRes;
bRes += bStabPRes;

{
	Vh4P2 [RES, RESY, RESZ, RESP];
	mpi(cout<<"Resuelvo el residuo"<<endl;)
	RES[] = MRes^-1*bRes;  
	ResG(iRB) = normX(RES[], MPescX);
}