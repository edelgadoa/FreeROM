///////////////
// FreeROM 
/////////////// 
int I=i+1; // Para ajustar los indices

// Inicializamos los vectores donde almacenaré los residuos y estimadores de cada iteración del Greedy
real[int] TauG(Ntrain), DeltaG(Ntrain), ResG(Ntrain); 

// Inicializamos las matrices para el problema reducido
matrix DiffRBi, DivPRBi, DivPRBTi, ConvLRBi, PenRBi, DTRBi;
real[int,int] ConvRBi(I, I), SmagoRBi(I, I), StabPRBi(I, I);
			   			   			 			  
// Inicializamos los segundos miembros
real[int] bDTRBi(I); 

// Matrices fijas 
DiffRBi = DiffRBG;
DivPRBi = DivPRBG;
DivPRBTi = DivPRBi'; DivPRBTi = (-1)*DivPRBTi;
ConvLRBi = ConvLRBG;
PenRBi = PenRBG;
DTRBi = DTRBG;
matrix StabhkRBs = StabhkRBG;

// Comenzamos el bucle del greedy 

for (int iRB=0; iRB<Ntrain; iRB++){
	
	mpi(cout<<"----Reynolds= "<<MuTrain(iRB)<<"-----------"<<endl;)
	real nuRB = 1./MuTrain(iRB);
	
	// Resuelvo el problema 
	real[int] uRBv(2*I); // Vectores donde almaceno la Solución Reducida
	real[int,int] MRB(2*I,2*I); // Matriz para resolver el sistema
	real[int] bRB(2*I); // Segundo miembro para resolver el sistema
	
	include "RB_LPS_VMSSmago.idp"
	
	// Reconstruyo la solución reducida
	Vh3P2 [uRB, uRBY, uRBZ];
	VhP2 uRBP;
	
	real[int,int] MBaseVint(Vh3P2.ndof, I), MBasePint(VhP2.ndof, I); // Matrices intermedias
	MBaseVint = MBaseV(0:Vh3P2.ndof-1, 0:I-1);
	MBasePint = MBaseP(0:VhP2.ndof-1, 0:I-1);

	
	uRB[] = MBaseVint*uRBv(0:I-1);
	uRBP[] = MBasePint*uRBv(I:2*I-1);
	
	if(MuTrain(iRB)==Reynolds){
		// Vh3P2G [uRBG, uRBGY, uRBGZ];
		// VhP2G uRBPG;
		// uRBG[] = reduceSolution(uRB[], uRBG[], MatVh3P2.D, mapVh3P2);
		// uRBPG[] = reduceSolution(uRBP[], uRBPG[], MatVhP2.D, mapVhP2);
		// savevtk("uRB"+MuTrain(iRB)+".vtu", ThG, [uRBG, uRBGY, uRBGZ], uRBPG, dataname = "Velocity Pressure", order=fforder);
		mpi(cout<<uRBv<<endl;)
	}
	
	VhP1dc uRBdX, uRBdY, uRBdZ, uRBYdX, uRBYdY, uRBYdZ, uRBZdX, uRBZdY, uRBZdZ;
	uRB[] += LevV[]; 
	
	{ 
		VhP2 uRBvel, uRBvelY, uRBvelZ;
		uRBvel = uRB; uRBvelY = uRBY; uRBvelZ = uRBZ;
	
		uRBdX[] = DXP2IPh * uRBvel[]; uRBdY[] = DYP2IPh * uRBvel[];	uRBdZ[] = DZP2IPh * uRBvel[];
		uRBYdX[] = DXP2IPh * uRBvelY[]; uRBYdY[] = DYP2IPh * uRBvelY[]; uRBYdZ[] = DZP2IPh * uRBvelY[];
		uRBZdX[] = DXP2IPh * uRBvelZ[]; uRBZdY[] = DYP2IPh * uRBvelZ[];	uRBZdZ[] = DZP2IPh * uRBvelZ[];
	}
	
	
	// Calculo el estimador de error 

	// Norma del Residuo
	include "Residuo_LPS_VMSSmago.idp"	
	mpi(cout<<"ResN = "<<ResG(iRB)<<endl;)
	
	// Beta_h
	real Betah = fbeta(gammaI, muI, MuTrain(iRB)); 
	
	// Rho_t
	real RhoT = 2*CSobolev^2;
	
	// Tau_N
	TauG(iRB) = (4 * RhoT * ResG(iRB))/(Betah^2);
	mpi(cout<<"TauN = "<< TauG(iRB)<<endl;)
	
	// DeltaN
	if (TauG(iRB) < 1){
		DeltaG(iRB) = Betah/(2*RhoT)*(1-sqrt(1-TauG(iRB)));
	}
	else{
		DeltaG(iRB) = Betah/(4*RhoT)*TauG(iRB);
	}
	
}

mpi(cout<<"Delta"<<DeltaG<<endl;)
DeltaN(i) = DeltaG.max;
TauN(i) = TauG.max;
MuBest(i+1) = MuTrain(DeltaG.imax);
