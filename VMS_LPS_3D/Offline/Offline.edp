///////////////
// FreeROM 
/////////////// 

/////////////////////////////////////////
/////////// FASE OFFLINE ////////////////
//// NS + VMS-Smago + Estab Pres (LPS)///
/////////////////////////////////////////
/////////// NO SUPREMIZER ///////////////
/////////////////////////////////////////

verbosity = 0;

// Codigo para calcular la fase offline VMS-Smago con estabilizacion LPS en la presion. 
// Elemento Finito P2-P2 (no estable) en vel-pres SIN SUPREMIZER

// Cargamos librerias
load "iovtk" 
load "lapack"
load "PETSc";
// load "MUMPS_mpi" // Para calcular la constante de Sovolev en paralelo

macro dimension()3 //EOM
include "macro_ddm.idp"

// Creamos la Malla
include "../Auxiliary/CuboNxNyNz.idp"

// Cargamos macros, funciones i/o
include "../Auxiliary/Macros_3D.idp"
include "../Auxiliary/search.idp"
include "../Auxiliary/IO.idp" 
include "../Auxiliary/Functions_MP.idp"


// Definimos los parametros a usar
include "../Auxiliary/Parametros_3D.idp"

// Definimos los espacios de EF
include "../FOM/FE_Spaces_NoStab_VP_3D.idp"


VhP0 mk,tKcod,tKTcod,tau;

varf med(unused,v) = intN(Th)(1.*v);
real[int] medk = med(0,VhP0);
mk[] = sqrt(medk);

VhP0 hk = hTriangle;
real hT, hTL = hk[].max;
mpiAllReduce(hTL, hT, mpiCommWorld, mpiMAX); //El máximo de los diámetros de la malla

VhP0G mkG, hkG;
hkG = hTriangle;
varf medG(unused,v) = intN(ThG)(1.*v);
real[int] medkG = medG(0,VhP0G);
mkG[] = sqrt(medkG);


//Construyo el vector que bloqueará el residuo de SMAGO
varf CC([un1,un2,un3,unp],[vvel1,vvel2,vvel3,vpres]) = on(1,2, un1=1,un2=1,un3=1); 

real[int] cc = CC(0,Vh4P2);
for (int gf=0; gf<Vh4P2.ndof; gf++){
	cc(gf) = (cc(gf)>10^10) ? 0 : 1;
} 

mapVh4P2 = restrict(Vh4P2,Vh4P2G,myN2o);
mapVh3P2 = restrict(Vh3P2,Vh3P2G,myN2o);
mapVhP2 = restrict(VhP2,VhP2G,myN2o);
mapVhP1dc = restrict(VhP1dc,VhP1dcG,myN2o);
mapVhP0 = restrict(VhP0, VhP0G, myN2o);


real[int] DP0, DP1dc;
PartitionCreate(Th, DP0, P0);
PartitionCreate(Th, DP1dc, P1dc);


{
	macro def(i) [i, i#Y, i#Z, i#P] // EOM 
	macro init(i) [i, i, i, i] // EOM
	MatCreate(Th, MatVh4P2, [P2, P2, P2, P2]); 
}

MatCreate(Th, MatVhP1dc, P1dc);
MatCreate(Th, MatVhP2, P2);
	
{
	macro def(i) [i, i#Y, i#Z] // EOM
	macro init(i) [i, i, i] // EOM
	MatCreate(Th, MatVh3P2, [P2, P2, P2]); 
}

// Definimos las matrices de interpolacion y derivacion
include "../FOM/Interp_matrix_NoStab_VP_3D.idp"
include "../FOM/Interp_matrix_NoStab_VP_3D_Global.idp" //Es necesario?

// Incluimos el levantamiento
include "../FOM/Levantamiento_3D.idp"

// Defino funciones para ortonormalizar y productos escalares / normas
include "../Auxiliary/Normas_3D.idp"

// Cargo las funciones del RBF
include "../Auxiliary/betas.idp"


//Definicion de Constantes y variables varias
real[int] MuBest(Nbases + 1), DeltaN(Nbases), TauN(Nbases), ErrorVN(Nbases), ErrorPN(Nbases);


// Leo los valores de los parámetros para los cuales he calculado ya Snapshots
readvector("../Snapshots/Snapshots.txt", real, ReSnapshots, 0);

//////////////////////////////////////////
////DEFINICION DE LOS ESPACIOS DE BR//////
//////////////////////////////////////////

VhP2[int] BaseP(Nbases);
Vh3P2[int] [BaseV, BaseVY, BaseVZ](Nbases);

VhP2[int] BaseVel(Nbases);
VhP2[int] BaseVelY(Nbases);
VhP2[int] BaseVelZ(Nbases);

Vh3P2[int] [FBaseV, FBaseVY, FBaseVZ](Nbases);

VhP2[int] FBaseVel(Nbases), FBaseVelY(Nbases), FBaseVelZ(Nbases);

// VhP1dc[int] FPdX(Nbases), FPdY(Nbases), FPdZ(Nbases);

VhP1dc[int] FVdX(Nbases), FVdY(Nbases), FVdZ(Nbases);
VhP1dc[int] FVYdX(Nbases),FVYdY(Nbases), FVYdZ(Nbases);
VhP1dc[int] FVZdX(Nbases),FVZdY(Nbases), FVZdZ(Nbases);

// Defino las matrices donde almaceno los snapshots por columnas, para la reconstrucción de la solución reducida
real[int, int] MBaseV(Vh3P2.ndof, Nbases), MBaseP(VhP2.ndof, Nbases);
MBaseV = 0;
MBaseP = 0;

	
/////////////////////////////////////////
// Cargo los elementos del EIM (tanto para Smago como para LPS)
include "EIM_Data.idp"


// Cargo los datos del RBF
readvector("../RBF/BetaMuI.txt", real, BetaI, 0);
readvector("../RBF/vMuI.txt", real, muI, 0);
readvector("../RBF/gammaI.txt", real, gammaI, 0);

// Inicializo las matrices para la fase offline
include "Allocate_matrices.idp"


/////////////////////////////////////////
// Comenzamos con el Algoritmo Greedy

Reynolds = 1000; // Inicializamos el Reynolds, y todas las variables para resolver el problema de EF
MuBest(0) = Reynolds;

// Calculo la constante de Sobolev


real CSobolev;
include "Sobolev_3D.idp"


VhP2 uFijo, uFijoY, uFijoZ; //Para la normaT
VhP1dc uFijodX, uFijodY, uFijodZ, uFijoYdX, uFijoYdY, uFijoYdZ, uFijoZdX, uFijoZdY, uFijoZdZ;

Vh4P2 [uEF, uEFY, uEFZ, uEFP]; // Serán las soluciones para el Re_fijo
VhP1dc uEFdX, uEFdY, uEFdZ, uEFYdX, uEFYdY, uEFYdZ, uEFZdX, uEFZdY, uEFZdZ; //las derivadas de uEF del paso anterior CON LEVANTAMIENTO
VhP2 uprevL, uprevLY, uprevLZ; // La sol del paso anterior con Levantamiento
VhP1dc Gr; // Para el termino de VMS-Smagorinsky


if (search(ReSnapshots, Refijo, 1.e-5)==-1){
	// Si no lo he calculado todavía, lo calculo
	Reynolds = Refijo;
	include "../FOM/FE_VMS_LPS_3D.idp"

	uFijo = uEF + Lev;
	uFijoY = uEFY + LevY;
	uFijoZ = uEFZ + LevZ;
}
else{
	
	VhP2G SnapX, SnapY, SnapZ;
	readvector("../Snapshots/Vx_" + Refijo + ".txt", real, SolX, 0);
	SnapX[] = SolX;
	readvector("../Snapshots/Vy_" + Refijo + ".txt", real, SolY, 0);
	SnapY[] = SolY;
	readvector("../Snapshots/Vz_" + Refijo + ".txt", real, SolZ, 0);
	SnapZ[] = SolZ;
	
	uFijo[] = SnapX[](mapVhP2);
	uFijoY[] = SnapY[](mapVhP2);
	uFijoZ[] = SnapZ[](mapVhP2);
	
	uFijo = uFijo + Lev;
	uFijoY = uFijoY + LevY;
	uFijoZ = uFijoZ + LevZ;

}

// Defino los diferentes productos escalares para construir las matrices reducidas
include "Scalar_Product_Matrices.idp"

for (int i=0; i<Nbases; i++){
	
	if (search(ReSnapshots, Reynolds, 1.e-5)==-1){
		// Si no lo he calculado todavía, lo calculo

		include "../FOM/FE_VMS_LPS_3D.idp"	
		
		[BaseV[i], BaseVY[i], BaseVZ[i]] = [uEF, uEFY, uEFZ];
		BaseP[i] = uEFP;
		
		BaseVel[i] = uEF; BaseVelY[i] = uEFY; BaseVelZ[i] = uEFZ;// Para construir las funciones filtradas después
		
		real[int] ReSnapshotsNEW(ReSnapshots.n + 1); //Voy a redefinir el vector con los Snapshots calculados
		ReSnapshotsNEW(0:ReSnapshots.n - 1) = ReSnapshots;
		ReSnapshotsNEW(ReSnapshots.n) = Reynolds;
		
		
		write("../Snapshots/Snapshots.txt", ReSnapshotsNEW, 0, 0); 
		
		VhP2G SnapX, SnapY, SnapZ, SnapP;
		
		SnapX[] = reduceSolution(BaseVel[i][], SnapX[], MatVhP2.D, mapVhP2);
		SnapY[] = reduceSolution(BaseVelY[i][], SnapY[], MatVhP2.D, mapVhP2);
		SnapZ[] = reduceSolution(BaseVelZ[i][], SnapZ[], MatVhP2.D, mapVhP2);
		SnapP[] = reduceSolution(BaseP[i][], SnapP[], MatVhP2.D, mapVhP2);
		
		// Guardamos los snapshots 
		write("../Snapshots/Vx_" + Reynolds + ".txt", SnapX[], 0, 0);
		write("../Snapshots/Vy_" + Reynolds + ".txt", SnapY[], 0, 0);	
		write("../Snapshots/Vz_" + Reynolds + ".txt", SnapZ[], 0, 0);	
		write("../Snapshots/P_" + Reynolds + ".txt", SnapP[], 0, 0);
		
	}
	else{
		VhP2G SnapX, SnapY, SnapZ, SnapP;
		
		readvector("../Snapshots/Vx_" + Reynolds + ".txt", real, SolX, 0);
		SnapX[] = SolX;
		BaseVel[i][] = SnapX[](mapVhP2); //Cargo las bases en local
		readvector("../Snapshots/Vy_" + Reynolds + ".txt", real, SolY, 0);
		SnapY[] = SolY;
		BaseVelY[i][] = SnapY[](mapVhP2);
		readvector("../Snapshots/Vz_" + Reynolds + ".txt", real, SolZ, 0);
		SnapZ[] = SolZ;
		BaseVelZ[i][] = SnapZ[](mapVhP2);
		readvector("../Snapshots/P_" + Reynolds + ".txt", real, SolP, 0);
		SnapP[] = SolP;

		BaseP[i][] = SnapP[](mapVhP2);		
	
		[BaseV[i], BaseVY[i], BaseVZ[i]] = [BaseVel[i], BaseVelY[i], BaseVelZ[i]];
			
	}
	
	include "GramSchmidt.idp"
	
	VhP2[int] BasePi(i+1);
	Vh3P2[int] [BaseVi, BaseViY, BaseViZ](i+1);
	real[int, int] MBaseVi(MatVh3P2.n, i+1), MBasePi(MatVhP2.n, i+1);

	for (int k=0;k<=i;k++){
		BasePi[k][] = BaseP[k][];
		BaseVi[k][] = BaseV[k][];
	}
	
	MBaseV(:,i) = BaseV[i][];
	MBaseP(:,i) = BaseP[i][];


	// Almaceno en las matrices las Bases ortonormalizadas por columnas (en numeracion PETSC) para la construcccion de matrices
	ChangeNumbering(MatVhP2, BasePi, MBasePi); 
	ChangeNumbering(MatVh3P2, BaseVi, MBaseVi);


	// Actualizo las soluciones filtradas
	 FBaseV[i][] = IPh3P23P1 * BaseV[i][];
	//
	 FBaseVel[i][] = IPhP2P1*BaseVel[i][]; FBaseVelY[i][] = IPhP2P1*BaseVelY[i][]; FBaseVelZ[i][] = IPhP2P1*BaseVelZ[i][];
	//
	 FVdX[i][] = DXP2*FBaseVel[i][]; FVdY[i][] = DYP2*FBaseVel[i][]; FVdZ[i][] = DZP2*FBaseVel[i][];
	 FVYdX[i][] = DXP2*FBaseVelY[i][]; FVYdY[i][] = DYP2*FBaseVelY[i][]; FVYdZ[i][] = DZP2*FBaseVelY[i][];
	 FVZdX[i][] = DXP2*FBaseVelZ[i][]; FVZdY[i][] = DYP2*FBaseVelZ[i][]; FVZdZ[i][] = DZP2*FBaseVelZ[i][];
	 
	 FVdX[i][] .*= DP1dc; FVdY[i][] .*= DP1dc; FVdZ[i][] .*= DP1dc;
	 FVYdX[i][] .*= DP1dc; FVYdY[i][] .*= DP1dc; FVYdZ[i][] .*= DP1dc;
	 FVZdX[i][] .*= DP1dc; FVZdY[i][] .*= DP1dc; FVZdZ[i][] .*= DP1dc;
	 
	//
	//FPdX[i][] = IPhDXP2*BaseP[i][]; FPdY[i][] = IPhDYP2*BaseP[i][];
	//
	
	// Defino el nuevo producto escalar para la convección
    Vh3P2 [BV, BVY, BVZ];
    BV[] = BaseV[i][];
	varf ConvPE([uu,uuY,uuZ], [v,vY,vZ]) = int3d(Th)(Antisimetric(BV,uu,v));
	{
		matrix TMP = ConvPE(Vh3P2,Vh3P2);
		macro def(i) [i, iY, iZ] // EOM
		macro init(i) [i, i, i] // EOM
		MatCreate(Th, ConvPEM[i], [P2,P2,P2]);
		ConvPEM[i] = TMP; 
	}
	
	
	include "MatricesOffline.idp"
		
	// Hago el algoritmo Greedy para determinar el nuevo valor del parámetro
	include "Greedy.idp"
	
	// Actualizo el nuevo Reynolds
	Reynolds = MuBest(i+1);
	mpi(cout<<"New Reynolds: "<<Reynolds<<endl;)
	
	mpi(cout<<"-------FIN DE LA ITERACION "<<i<<"-------"<<endl;)
	
	if (DeltaN(i)<epsGreedy){
		NRB = i + 1;
		include "Save_Matrices.idp"
		break;
	}
}
mpi(cout<<"================================"<<endl<< "--------FIN DEL METODO RB-------"<<endl<<"================================"<<endl;)
