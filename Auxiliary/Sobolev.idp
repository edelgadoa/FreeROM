///////////////
// FreeROM 
/////////////// 

include "powermethod.idp"

readvector("../../Snapshots/VMS_Smago_LPS_pres_2D/Snapshots.txt", real, ReSnapshots, 0);

VhP2 uF,uFY;
VhP1dc uFdX, uFdY, uFYdX, uFYdY;
VhP2 SnapX, SnapY, SnapP;

// Cargo o calculo el snapshot para el Reynolds fijo de la norma T
if (search(ReSnapshots, Refijo, 1.e-9)==-1){
	// Defino las variables que usaremos para obtener la solucion de EF
	Vh3P2 [uEF, uEFY, uEFP];
	VhP1dc uEFdX, uEFdY, uEFYdX, uEFYdY;
	VhP2 uprevL, uprevLY; // La sol del paso anterior con Levantamiento
	VhP1dc Gr; // Para el termino de VMS-Smagorinsky


	include "../../Full_Order/VMS_Smago_LPS_pres_2D/FE_VMS_LPS_2D.idp"
	
	real[int] ReSnapshotsNEW(ReSnapshots.n + 1); //Voy a redefinir el vector con los Snapshots calculados
	ReSnapshotsNEW(0:ReSnapshots.n - 1) = ReSnapshots;
	ReSnapshotsNEW(ReSnapshots.n) = Refijo;
	
	write("../../Snapshots/VMS_Smago_LPS_pres_2D/Snapshots.txt", ReSnapshotsNEW, 0, 0); //0,0 -> No append + No mostrar por pantalla 
	
	SnapX = uEF;
	SnapY = uEFY;
	SnapP = uEFP;
		
	// Guardamos los snapshots 
	write("../../Snapshots/VMS_Smago_LPS_pres_2D/Vx_" + Refijo + ".txt", SnapX[], 0, 0);
	write("../../Snapshots/VMS_Smago_LPS_pres_2D/Vy_" + Refijo + ".txt", SnapY[], 0, 0);	
	write("../../Snapshots/VMS_Smago_LPS_pres_2D/P_" + Refijo + ".txt", SnapP[], 0, 0);

	SnapX = SnapX + Lev;
	SnapY = SnapY + LevY;
	
	uF[] = IPhP2P1*SnapX[];
	uFdX[] = DXP2*uF[]; 
	uFdY[] = DYP2*uF[];
	
	uFY[] = IPhP2P1*SnapY[];
	uFYdX[] = DXP2*uFY[]; 
	uFYdY[] = DYP2*uFY[];
	
}
else{

	readvector("../../Snapshots/VMS_Smago_LPS_pres_2D/Vx_" + Refijo + ".txt", real, SolX, 0);
	SnapX[] = SolX;
	readvector("../../Snapshots/VMS_Smago_LPS_pres_2D/Vy_" + Refijo + ".txt", real, SolY, 0);
	SnapY[] = SolY;
	readvector("../../Snapshots/VMS_Smago_LPS_pres_2D/P_" + Refijo + ".txt", real, SolP, 0);
	SnapP[] = SolP;
	
	SnapX = SnapX + Lev;
	SnapY = SnapY + LevY;
	
	uF[] = IPhP2P1*SnapX[];
	uFdX[] = DXP2*uF[]; 
	uFdY[] = DYP2*uF[];
	
	uFY[] = IPhP2P1*SnapY[];
	uFYdX[] = DXP2*uFY[]; 
	uFYdY[] = DYP2*uFY[];
	
}

varf prodT([uu,uuY],[v,vY])= int2d(Th)(((1./Refijo)+((CS*hTriangle)^2)*MOD(uF))*GradUGradV(uu,v)) + on(1,2,3,4, uu=0,uuY=0);

matrix T, Zu;
T = prodT(Vh2P2, Vh2P2);

//macro normsup(vh) int2d(Th)(vh^2)//EOM

real CSerror = 10, CSeps = 1.e-5;

real rhok0 = 20, rhok1;

VhP2 u0, u0Y;
VhP2 zuk;

u0 = uF;
u0Y = uFY;

// Fix point
for(int ir=0;ir<1000 && CSerror>CSeps;ir++)
{
	zuk = UporV(u0,u0);
	zuk[] /= sqrt(int2d(Th)(zuk^2));
//	 {real normL2=int2d(Th)(zuk^2);
//	  cout<<"norm u: "<<normL2<<endl;} // Debe ser parecido a 1

	varf prodzu([uu,uuY],[v,vY])=int2d(Th)(zuk*UporV(uu,v));
	Zu = prodzu(Vh2P2, Vh2P2);
	
	real[int] autovec = powermethod(Zu,T);
	rhok1 = autovec(0);
	u0[] = autovec(1:autovec.n-1);
	// plot(defVel(u0),value=1,wait=1);
	CSerror = abs((rhok1-rhok0)/rhok1);
	// cout << "PM: " << CSerror<< endl;
	rhok0 = rhok1;
}

real CSobolev = rhok1;
cout<<CSobolev<<endl;