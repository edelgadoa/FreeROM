///////////////
// FreeROM 
/////////////// 

//////////////////////////////////////////////////
/////////// FASE OFFLINE /////////////////////////
//// Unsteady NS + Smago + BC periodicas /////////
//////////////////////////////////////////////////

verbosity = 0;

// Codigo para calcular la fase offline VMS-Smago con estabilizacion LPS en la presion. 
// Elemento Finito P2-P2 (no estable) en vel-pres SIN SUPREMIZER

// Cargamos librerias
load "iovtk" 
load "lapack"
load "PETSc"

// Cargamos macros, funciones i/o
include "../Auxiliary/IO.idp"
include "../Auxiliary/Macros_2D.idp"
include "../Auxiliary/macro_ddm.idp"
include "../Auxiliary/search.idp"

// Definimos los parametros a usar
include "../Auxiliary/Parametros_2D.idp"

// Creamos la Malla
include "../Auxiliary/Malla.idp"

// Definimos los espacios de EF
include "../FOM/FE_Spaces_NoStab_VP_2D.idp"

include "../Auxiliary/Normas.idp"

////////////////////////////////////////////////////////////////////////////////////

// Carga de datos importantes
real tS0 = 7-0.332;
real tSF = 7;
real niS = (tSF-tS0)/dt+1;

//Definicion de Constantes y variables varias
real[int,int] MBaseV(Vh2P2.ndof,niS+1);    real[int] MediaV(Vh2P2.ndof);     MediaV = 0.;
real[int,int] MBaseVTau(Vh2P2.ndof,niS+1); real[int] MediaVTau(Vh2P2.ndof);  MediaVTau = 0.;
real[int,int] MBaseVDTau(VhP2.ndof,niS+1); real[int] MediaVDTau(Vh2P2.ndof); MediaVDTau = 0.;
real[int,int] MBaseConv(Vh2P2.ndof,niS+1); real[int] MediaConv(Vh2P2.ndof);  MediaConv = 0.;


for(int iter=0; iter <niS; iter++){
    real ti = tS0+iter*dt;
    write("Output.txt","t = "+ti,1,1);

    VhP2 aux, aux2;
    // Snapshots
    Vh3P2 [SnapX,SnapY,SnapP];
    readvector("../Snapshots/Vx_" + Reynolds + "_t_" +ti+ ".txt", real, SolX, 0);
    readvector("../Snapshots/Vy_" + Reynolds + "_t_" +ti+ ".txt", real, SolY, 0);
    SnapX[] = SolX; SnapY[] = SolY;

    Vh2P2 [SX,SY];
    [SX,SY] = [SnapX, SnapY];

    //aux = SX^2+SY^2;
    //plot(aux,fill=1,value=1,cmm="t="+ti);

    MBaseV(:,iter) = SX[];
    MediaV += 1/(niS+1)*SX[];

    // Estabilizador
    VhP1 tau;
    readvector("Estabilizador/tau_" + Reynolds + "_t_" +ti+ ".txt", real, tauvec, 0);
    tau[] = tauvec;
    //plot(tau,fill=1,value=1,cmm="t="+ti);

    // Definicion de elementos para hacer la POD
    // u por tau (2 componentes)
    Vh2P2 [uxtau, uytau];
    aux = tau*SX; aux2 = tau*SY;
    [uxtau, uytau] = [aux,aux2];

    //aux = uxtau^2+uytau^2;
    //plot(aux,fill=1,value=1,cmm="t="+ti);
    
    MBaseVTau(:,iter) = uxtau[];
    MediaVTau += 1/(niS+1)*uxtau[];

    // Conveccion (2 componentes)
    Vh2P2 [zx,zy];
    VhP2 zzx,zzy;
    aux = SX^2; 
    aux2 = SX*SY;
    zzx = dx(aux)+dy(aux2);
    aux = SY^2;
    zzy = dx(aux2)+dy(aux);
    [zx,zy] = [zzx,zzy];

    //aux = zx^2+zy^2;
    //plot(aux,fill=1,value=1,cmm="t="+ti);

    MBaseConv(:,iter) = zx[];
    MediaConv += 1/(niS+1)*zx[];
    
    // u por gradiente de tau (1 componente)

    VhP2 udtau;
    udtau = SX*dx(tau)+SY*dy(tau);

    MBaseVDTau(:,iter) = udtau[];
    MediaVDTau += 1/(niS+1)*udtau[];

    //plot(udtau,fill=1,value=1,cmm="t="+ti);
}

// Hay que quitar la media

write("Output.txt","Substracting and saving mean value ...",1,1);
for(int i=0; i <niS; i++){
    MBaseV(:,i) -= MediaV; 
    MBaseVTau(:,i) -= MediaVTau; 
    MBaseVDTau(:,i) -= MediaVDTau; 
    MBaseConv(:,i) -= MediaConv; 
}

write("Bases/MediaV_" + Reynolds + "_PODtol_" +(-log10(epsPOD))+ ".txt", MediaV, 0, 0);
write("Bases/MediaVTau_" + Reynolds + "_PODtol_" +(-log10(epsPOD))+ ".txt", MediaVTau, 0, 0);
write("Bases/MediaConv_" + Reynolds + "_PODtol_" +(-log10(epsPOD))+ ".txt", MediaConv, 0, 0);
write("Bases/MediaVDTau_" + Reynolds + "_PODtol_" +(-log10(epsPOD))+ ".txt", MediaVDTau, 0, 0);

write("Output.txt","Mean values OK.",1,1);


// Calculamos la POD
write("Output.txt","Computing the complete basis...",1,1);
int NbasesV, NbasesVTau, NbasesConv, NbasesVDTau;
Vh2P2[int] [BaseV,BaseVY](niS);
Vh2P2[int] [BaseVTau,BaseVTauY](niS);
Vh2P2[int] [BaseConv,BaseConvY](niS);
VhP2[int] BaseVDTau(niS);
{
    varf scalar2L2([u,uY],[v,vY]) = int2d(Th)(u*v+uY*vY);
    varf scalarL2(u,v) = int2d(Th)(u*v);
	matrix MScalar = scalarL2(VhP2, VhP2);
	matrix MScalar2 = scalar2L2(Vh2P2, Vh2P2);
    include "POD.idp" // Hay que hacerla en paralelo, para que todo vaya bien (TODO)
    POD(MBaseV,MScalar2,BaseV,NbasesV,autovalV);
    POD(MBaseVTau,MScalar2,BaseVTau,NbasesVTau,autovalVTau);
    POD(MBaseConv,MScalar2,BaseConv,NbasesConv,autovalVConv);
    POD(MBaseVDTau,MScalar,BaseVDTau,NbasesVDTau,autovalVDTau);

}
write("Output.txt","After POD, for vel "+NbasesV+" basis, for veltau "+NbasesVTau+" basis, for conv "+NbasesConv+", and for ugradtau "+NbasesVDTau+".",1,1);

real[int,int] BV(Vh2P2.ndof,NbasesV);
real[int,int] BVTau(Vh2P2.ndof,NbasesVTau);
real[int,int] BConv(Vh2P2.ndof,NbasesConv);
real[int,int] BVDTau(VhP2.ndof,NbasesVDTau);
for(int i=0; i<NbasesV; i++){
    BV(:,i) = BaseV[i][];
    //VhP2 modvv;
    //modvv = BaseV[i]^2+BaseVY[i]^2;
    //plot(modvv,fill=1,wait=1,value=1,cmm = "Modo "+(i+1));
    //plot([BaseV[i],BaseVY[i]],fill=1,wait=1,value=1,cmm = "Modo "+(i+1));
}


for(int i=0; i<NbasesVTau; i++){
    BVTau(:,i) = BaseVTau[i][];
    //plot([BaseVTau[i],BaseVTauY[i]],fill=1,wait=1,value=1,cmm = "Modo "+(i+1));
}

for(int i=0; i<NbasesConv; i++){
    BConv(:,i) = BaseConv[i][];
    //plot([BaseConv[i],BaseConvY[i]],fill=1,wait=1,value=1,cmm = "Modo "+(i+1));
}

for(int i=0; i<NbasesVDTau; i++){
    BVDTau(:,i) = BaseVDTau[i][];
    //plot(BaseVDTau[i],fill=1,wait=1,value=1,cmm = "Modo "+(i+1));
}


write("Bases/BaseV_" + Reynolds + "_PODtol_" +(-log10(epsPOD))+ ".txt", BV, 0, 0);
write("Bases/BaseVTau_" + Reynolds + "_PODtol_" +(-log10(epsPOD))+ ".txt", BVTau, 0, 0);
write("Bases/BaseConv_" + Reynolds + "_PODtol_" +(-log10(epsPOD))+ ".txt", BConv, 0, 0);
write("Bases/BaseVDTau_" + Reynolds + "_PODtol_" +(-log10(epsPOD))+ ".txt", BVDTau, 0, 0);